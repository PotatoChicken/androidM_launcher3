apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {

        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion

        defaultConfig {
            applicationId "com.android.launcher3"
            minSdkVersion 17
            targetSdkVersion 23

            testApplicationId "com.android.launcher3.tests"
            testInstrumentationRunner "android.test.InstrumentationTestRunner"
            testHandleProfiling true
            testFunctionalTest true
        }



    lintOptions{
        abortOnError true

        check 'NewApi', 'InlinedApi'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/MANIFEST.MF'
    }


    signingConfigs{
        release{

        }

        red{

        }

        blue{

        }
    }
    sourceSets{
        main{
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']

            proto.srcDirs 'protos/'
        }

        free{
            //修改free版本中的icon/颜色
            res.srcDirs = ['src/free/res']
        }
    }

    /*
     * flavorDimensions 把productFlavors分成不同的维度，不同的维度之间进行组合
     * 下面将productFlavors分为两组，分别为color[red, blue]h和price[free, paid]
     * flavorDimensions数组有先后，不同的顺序组合成的BuildVariant不同
     *
     * price -> color -> defaultConfig
     */
    flavorDimensions "price", "color"

    /*
     * 注意 ProductFlavor 类型的 android.productFlavors.* 对象
     * 与 android.defaultConfig 对象的类型是相同的,
     * 就是说着它们拥有一样的属性。
     * defaultConfig 为所有的 flavor 提供基本的配置，每一个 flavor 都可以重写这些配置的值。
     */
    productFlavors {
        red {
            flavorDimension "color"
        }
        blue {
            flavorDimension "color"
        }
        free {
            flavorDimension "price"

        }
        paid {
            flavorDimension "price"
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
        }
        release {
            /*
             * 不同的版本使用不同的签名
             */
            productFlavors.red.signingConfig signingConfigs.red
            productFlavors.blue.signingConfig signingConfigs.blue
            //signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    /*
     *  忽略某个变体,变体过滤器
     *  下面是忽略release和blue的组合变体
     */
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')) {
            variant.getFlavors().each() { flavor ->
                if (flavor.name.equals('blue')) {
                    variant.setIgnore(true);
                }
            }
        }
    }

    /**
     * 修改apk名称
     */
    android.applicationVariants.each { variant ->
        variant.outputs.each { output ->
            File file = output.outputFile;
            output.outputFile = new File(file.parent, project.name + ".apk")
            output.zipAlign.doLast {
                delete fileTree(dir: output.outputFile.parent, include: '*unaligned*.apk')
                def now = new Date().format('yyyy');
                copy {
                    from output.outputFile
                    into output.outputFile.parent
                    rename(project.name + ".apk", project.name + "_V${defaultConfig.versionName}" + "_" + now + "_${variant.productFlavors[0].name}" + ".apk")
                }

            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2'

    //加入测试库
    testCompile 'junit:junit:4.12'
}


protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-2'
    }
}