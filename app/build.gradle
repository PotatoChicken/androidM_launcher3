apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
android {

        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion

        defaultConfig {
            applicationId "com.android.launcher3"
            minSdkVersion 16
            targetSdkVersion 23

            testApplicationId "com.android.launcher3.tests"
            testInstrumentationRunner "android.test.InstrumentationTestRunner"
        }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions{
        abortOnError true
    }

    sourceSets{
        main{
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']

            proto.srcDirs 'protos/'
        }

        free{
            //修改free版本中的icon
            res.srcDirs = ['src/free/res']
        }
    }

    /*
     * flavorDimensions 把productFlavors分成不同的维度，不同的维度之间进行组合
     * 下面将productFlavors分为两组，分别为color[red, blue]h和price[free, paid]
     * flavorDimensions数组有先后，不同的顺序组合成的BuildVariant不同
     *
     * price -> color -> defaultConfig
     */
    flavorDimensions "price", "color"
    productFlavors {
        red {
            flavorDimension "color"
        }
        blue {
            flavorDimension "color"
        }
        free {
            flavorDimension "price"
        }
        paid {
            flavorDimension "price"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2'
}


protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-2'
    }
}